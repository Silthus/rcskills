buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id 'idea'
    id 'io.freefair.lombok' version '5.3.0'
    id 'io.ebean' version '12.6.6'
    id 'kr.entree.spigradle' version '2.2.3'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"


if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 11
targetCompatibility = 11

ext {
    mcVersion = project.property("mcVersion")
}

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    depends = ['WorldGuard', 'ebean-wrapper']
    softDepends = ['art-framework', 'PlaceholderAPI', 'RCEconomy']
}

archivesBaseName = project.property("pluginName")

group project.property("group")

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url = 'https://jitpack.io' }
    spigot()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly spigot(mcVersion)

    api 'de.raidcraft.economy:wrapper:2.3.6'
    api 'net.silthus:ebean-wrapper:2.9.1'
    api 'com.github.Slikey:EffectLib:master-SNAPSHOT'
    api 'net.silthus.config-mapper:bukkit:1.1.4'

    api 'net.kyori:adventure-api:4.5.0'
    api 'net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT'
    api 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'
    api group: 'net.kyori', name: 'adventure-text-feature-pagination', version: '4.0.0-SNAPSHOT'
    api group: 'net.kyori', name: 'adventure-text-serializer-plain', version: '4.4.0'

    implementation 'com.sk89q.worldguard:worldguard-bukkit:7.0.4'
    implementation 'net.silthus.configlib:configlib-bukkit:2.6.0'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.1'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.9.0'
    implementation 'me.clip:placeholderapi:2.10.9'

    implementation 'org.codehaus.janino:janino:3.1.3'
    implementation group: 'org.codehaus.janino', name: 'commons-compiler', version: '3.1.3'

    implementation group: 'io.art-framework.core', name: 'api', version: '4.5.1'

    testImplementation 'io.ebean:ebean-test:12.6.6'

    testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    testImplementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.2'
    testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.23'
    testImplementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'
    testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.assertj:assertj-core:3.+'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.16:0.20.0'
    testImplementation 'org.jmockit:jmockit:1.49'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('co.aikar:acf-paper:'))
        include(dependency('net.silthus.configlib:configlib-bukkit:'))
        include(dependency('com.github.Slikey:EffectLib:'))
        include(dependency('net.kyori::'))
        include(dependency('org.codehaus.janino::'))
        include(dependency('de.raidcraft.economy:wrapper:'))
        include(dependency('com.github.stefvanschie.inventoryframework:IF:'))
        include(dependency('net.silthus.config-mapper:bukkit:'))
    }
    relocate 'co.aikar.commands', "shadow.${pluginName}.acf"
    relocate 'co.aikar.locales', "shadow.${pluginName}.locales"
    relocate 'de.exlll.configlib', "shadow.${pluginName}.configlib"
    relocate 'de.slikey.effectlib', "shadow.${pluginName}.effectlib"
    relocate 'net.kyori.adventure', "de.raidcraft.skills.text"
    relocate 'org.codehaus', "shadow.${pluginName}.codehaus"
    relocate 'de.raidcraft.economy.wrapper', "de.raidcraft.skills.economy"
    relocate 'com.github.stefvanschie.inventoryframework', "shadow.${pluginName}.inventoryframework"
    relocate 'net.silthus.configmapper', "de.raidcraft.skills.configmapper"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
    jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}"
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

tasks.withType(Javadoc) {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

defaultTasks 'build'